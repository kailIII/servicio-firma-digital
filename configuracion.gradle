import com.fasterxml.jackson.dataformat.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.3.2'
    }
}

project.ext {
    archivoConfiguracion = 'servicio.yml'
}

task expandirConfig(type: Copy) {
    def hostBaseDeDatos = obtenerValorDesdeVariableDeEntorno("IP_BASE_DATOS_PREPRO", "localhost")
    def clave = obtenerValorDesdeVariableDeEntorno("CLAVE_SNIESE_KEY_STORE", "sniese")
    def activarProxyProduccion = obtenerValorDesdeVariableDeEntorno("ACTIVAR_PROXY_PRODUCCION", "false")
    def urlBaseAutenticacion = obtenerValorDesdeVariableDeEntorno("URL_BASE_AUTENTICACION", "https://localhost:8447/")
    def urlBaseAutorizacion = obtenerValorDesdeVariableDeEntorno("URL_BASE_AUTORIZACION", "https://localhost:8443/")
    def cantidadInicialConnexiones = obtenerValorDesdeVariableDeEntorno("CANTIDAD_INICIAL_CONEXIONES", "1")
    def cantidadMinimaConnexiones = obtenerValorDesdeVariableDeEntorno("CANTIDAD_MINIMA_CONEXIONES", "1")
    def cantidadMaximaConnexiones = obtenerValorDesdeVariableDeEntorno("CANTIDAD_MAXIMA_CONEXIONES", "2")
    def metricas = obtenerValorDesdeVariableDeEntorno("METRICAS", "false")
    def metricasHost = obtenerValorDesdeVariableDeEntorno("IP_METRICAS", "localhost")
    def metricasPuerto = obtenerValorDesdeVariableDeEntorno("PUERTO_METRICAS", "2003")
    def configuracionMetricas = ""

    if(metricas.equals("true")){
        configuracionMetricas = "metrics:\n" +
                "  frequency: 5 seconds\n" +
                "  reporters:\n" +
                "    - type: graphite\n" +
                "      host: ${metricasHost}\n" +
                "      port: ${metricasPuerto}\n" +
                "      durationUnit: seconds\n" +
                "      rateUnit: minutes\n" +
                "      prefix: servicio-firma-digital"
    }

    from('.')
    include('**/*.yml.template')
    include('**/*.ini.template')
    rename { String fileName -> fileName.replace('.template', '') }
    expand(
            host: hostBaseDeDatos,
            claveSnieseKeyStore: clave,
            proxyActivo: activarProxyProduccion,
            urlBaseAutenticacion: urlBaseAutenticacion,
            urlBaseAutorizacion: urlBaseAutorizacion,
            cantidadInicialConnexiones: cantidadInicialConnexiones,
            cantidadMinimaConnexiones: cantidadMinimaConnexiones,
            cantidadMaximaConnexiones: cantidadMaximaConnexiones,
            metricasHost: metricasHost,
            metricasPuerto: metricasPuerto,
            configuracionMetricas: configuracionMetricas
    )
    into('.')
}

task configurarServicio(dependsOn: expandirConfig) {
    doLast {
        def yamlConfig = new Yaml().load(new File(archivoConfiguracion).newReader())
        def dbConfig = yamlConfig.database
        project.ext['flyway.user'] = dbConfig.user
        project.ext['flyway.password'] = dbConfig.password
        project.ext['flyway.url'] = dbConfig.url
        project.ext['flyway.schemas'] = dbConfig.properties.'hibernate.default_schema'
    }
}

def obtenerValorDesdeVariableDeEntorno(key, valorPorDefecto) {
    if (System.getenv(key) != null) {
        return System.getenv(key)
    } else if (this.properties[key] != null) {
        return this.properties[key]
    }
    return valorPorDefecto
}
